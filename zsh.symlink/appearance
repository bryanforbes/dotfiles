# vim: set filetype=zsh:

case "$TERM" in
  xterm*|rxvt*)
    term_preexec () {
      print -Pn "\e]0;%n@%m: $1\a"  # xterm
    }
    term_precmd () {
      print -Pn "\e]0;%n@%m: %~\a"  # xterm
    }
    ;;
  screen*)
    term_preexec () {
      local CMD=${1[(wr)^(*=*|sudo|ssh|-*)]}
      echo -ne "\ek$CMD\e\\"
      print -Pn "\e]0;%n@%m: $1\a"  # xterm
    }
    term_precmd () {
      echo -ne "\ekzsh\e\\"
      print -Pn "\e]0;%n@%m: %~\a"  # xterm
    }
    ;;
esac
preexec_functions+=term_preexec
precmd_functions+=term_precmd

autoload colors; colors;
if [[ "$terminfo[colors]" -ge 8 ]]; then
	colors
fi

for color in RED GREEN YELLOW BLUE MAGENTA CYAN WHITE; do
	eval PR_$color='%{$terminfo[bold]$fg[${(L)color}]%}'
	eval PR_LIGHT_$color='%{$fg[${(L)color}]%}'
done
PR_RESET="%{$reset_color%}"

# Setup the prompt with pretty colors
setopt prompt_subst

PROMPT='${PR_GREEN}%n${PR_RESET} at ${PR_YELLOW}%m${PR_RESET} in ${PR_BLUE}%~${PR_RESET}
${vcs_info_msg_0_}%# '

# only eval dircolors if it exists
if [[ -a "$(whence dircolors)" ]]; then
	eval $(dircolors)
fi
