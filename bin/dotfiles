#!/usr/bin/env zsh

source $HOME/.dotfiles/zsh/common.zsh
source $HOME/.dotfiles/zsh/utilities.zsh

# Fix terminal config
function fixterm {
    if [[ $TERM == "xterm-kitty" && $(uname -s) == "Darwin" && ! -d $HOME/.terminfo/78/xterm-kitty ]]; then
        local tmpfile=$(mktemp /tmp/dotfiles.XXXXXX)
        infocmp xterm-kitty > $tmpfile
        tic -x -o $HOME/.terminfo $tmpfile &> /dev/null
        rm $tmpfile
    fi

    # Fix terminal definition so C-H works properly in neovim
    kbs=$(infocmp $TERM | grep -o 'kbs=[^,]\+')
    if [[ $kbs =~ "kbs=^[hH]" ]]; then
        logSub "Fixing backspace code in terminfo..."
        infocmp $TERM | sed 's/kbs=^[hH]/kbs=\\177/' > /tmp/$TERM.ti
        tic /tmp/$TERM.ti
        rm /tmp/$TERM.ti
    fi
}

# Rebuild bat cache
function dotfiles-bat {
    log "Rebuilding bat cache..."
    bat cache --build 2>&1 > /dev/null
}

# Link dotfiles into $HOME and $CONFIGDIR, update terminfo
function dotfiles-home {
    log "Updating home directory files..."

    for file in $HOME/.[a-z]*(@) $CONFIGDIR/[a-zA-Z]*(@); do
        if [[ ! -e $file ]]; then
            rm $file
            logSub "Removed invalid link $file"
        fi
    done

    for file in $DOTFILES/home/*(.); do
        link $file $HOME/.$file:t
    done

    for file in $DOTFILES/config/*(.,/); do
        link $file $CONFIGDIR/$file:t
    done

    makedir $CACHEDIR/tmux
    makedir $CACHEDIR/vim/sessions
    makedir $CACHEDIR/vim/swap
    makedir $CACHEDIR/vim/backup
    makedir $CACHEDIR/vim/undo
    makedir $ZCOMPDIR
    makedir $ZPLUGDIR
    makedir $ZFUNCDIR
    makedir $DATADIR/direnv/allow

    makedir $CONFIGDIR
    link $DOTFILES/vim $CONFIGDIR/nvim

    link $DOTFILES/vim $HOME/.vim

    # Fix the terminal definition so that C-H works properly in neovim. This
    # function may also need to be run for the tmux terminal type.
    fixterm
}

function dotfiles-brew {
    if [[ ! -w $HOMEBREW_BASE/bin ]]; then
        log "Skipping homebrew update (no write permissions)"
        return
    fi

    log "Updating brew packages..."

    logSub "Checking for missing brew packages..."

    local -U brew_packages
    local -A brew_packages_args

    brew_packages=(
        'asdf'       # dev tools management
        'bat'        # better cat
        'direnv'     # local environment management
        'fzf'        # fuzzy finder
        'git'        # update from system stock git
        'mosh'       # remote terminal application that allows roaming
        'neovim'     # better vim
        'node'       # NodeJS
        'python@3.8' # update from stock Python
        'ripgrep'    # better grep
        'tmux'       # terminal multiplexer
        'yarn'       # node package manager
        'zsh'        # newer zsh
    )

    brew_packages_args[mosh]='--HEAD'

    if [[ $OSTYPE == darwin* ]]; then
        brew_packages=(
            $brew_packages
            'coreutils'           # GNU coreutils
            'blueutil'            # bluetooth CLI
            'gnu-sed'             # GNU sed
            'gnu-tar'             # GNU tar
            'gnupg'               # update from stock gnupg
            'mas'                 # App Store CLI
            'font-iosevka'        # iosevka font
            'font-hack-nerd-font' # hack font with glyphs
            'kitty'               # GPU based terminal emulator
        )
    fi

    local installed_packages=(
        "${(@f)$(brew list --formula -1)}"
        "${(@f)$(brew list --casks -1)}"
    )

    for pkg in $brew_packages; do
        if ((! $installed_packages[(Ie)$pkg])); then
            logSub "Installing $pkg..."
            brew install $pkg ${(z)${brew_packages_args[$pkg]:-''}}
        fi
    done

    logSub "Updating installed brew packages..."
    brew upgrade

    # Remove git's included zsh completions in favor of the system completions
    if [[ -f $HOMEBREW_BASE/share/zsh/site-functions/_git ]]; then
        rm -f $HOMEBREW_BASE/share/zsh/site-functions/_git
    fi
}

# Update Mac App Store apps
function dotfiles-mas {
    logSub "Updating installed App Store apps..."
    mas upgrade
}

# Update zsh plugins
function dotfiles-zsh {
    log "Updating zsh plugins..."

    if [[ -z $ZPLUGDIR ]]; then
        err "ZPLUGDIR not defined"
        return
    fi

    cd $ZPLUGDIR
    for org in *; do
        cd $org
        for plugin in *; do
            cd $plugin
            head=$(git rev-parse HEAD)
            git pull -q --recurse-submodules
            git submodule update --remote
            if [[ $(git rev-parse HEAD) != $head ]]; then
                logSub "Updated $org/$plugin"
            fi
            cd ..
        done
        cd ..
    done
}

# Update user python2 and python3 packages
function dotfiles-python {
    if [[ ! -w $HOMEBREW_BASE/bin ]]; then
        log "Skipping python update (no write permissions)"
        return
    fi

    if [[ ! -e $HOMEBREW_BASE/bin/python ]] && [[ -e $HOMEBREW_BASE/bin/python3 ]]; then
        logSub "Creating python link..."
        link $HOMEBREW_BASE/bin/python3 $HOMEBREW_BASE/bin/python
    fi

    log "Updating python packages..."
    if [[ -f $HOMEBREW_BASE/bin/pip3 ]]; then
        $HOMEBREW_BASE/bin/pip3 install --upgrade pip setuptools > /dev/null
        $HOMEBREW_BASE/bin/pip3 install --upgrade --user pynvim neovim-remote > /dev/null
        logSub "Updated Python 3 packages"
    fi

    if [[ ! -d $HOME/.poetry ]]; then
        log "Installing poetry..."
        curl -sSL https://raw.githubusercontent.com/python-poetry/poetry/master/get-poetry.py | python3 -
    else
        log "Updating poetry..."
        $HOME/.poetry/bin/poetry self update
    fi

    log "Updating poetry completions..."
    $HOME/.poetry/bin/poetry completions zsh > $ZCOMPDIR/_poetry
}

# Update global npm packages
function dotfiles-node {
    if [[ ! -w $HOMEBREW_BASE/bin ]]; then
        log "Skipping node update (no write permissions)"
        return
    fi

    local node_path=$1
    if [[ -z $node_path ]]; then
        log "Updating global node packages..."
        node_path=$HOMEBREW_BASE/bin
    fi

    # if a node_path was provided, the function was called from another
    # function, so don't print the top-level message
    if [[ ! -x $node_path/npm ]]; then
        err "No npm executable"
        return
    fi

    # get list of outdated global packages
    local mods=($($node_path/npm --registry=https://registry.npmjs.org outdated -g --parseable))
    for mod in $mods; do
        # extract current and latest fields
        array=(${(@s/:/)mod})
        currentPkg=$array[3]
        latestPkg=$array[4]

        # extract current and latest versions, minus any prerelease tags
        currentVerParts=(${(@s/@/)currentPkg})
        currentVer=$currentVerParts[2]
        latestVerParts=(${(@s/@/)latestPkg})
        latestVer=$latestVerParts[2]

        if [[ $latestVer == 'linked' ]]; then
            logSub "Skipping $latestPkg"
            continue
        fi

        # read versions into arrays
        current=(${(@s/./)currentVer})
        latest=(${(@s/./)latestVer})

        # if latest is newer than current, install latest
        if (( latest[1] > current[1] )) || {
            (( latest[1] == current[1] )) &&
            (( latest[2] > current[2] )) || {
            (( latest[1] == current[1] )) &&
            (( latest[2] == current[2] )) &&
            (( latest[3] > current[3] ));
                    };
                }; then
            $node_path/npm install --registry=https://registry.npmjs.org --progress=false -g $latestPkg > /dev/null
            logSub "Installed $latestPkg"
        fi
    done
}

# Update tmux plugins
function dotfiles-tmux {
    log "Updating tmux plugins..."

    if [[ -z $TMUX_PLUGIN_MANAGER_PATH ]]; then
        err "TMUX_PLUGIN_MANAGER_PATH not defined"
        return
    fi

    if [[ -d $TMUX_PLUGIN_MANAGER_PATH/tpm ]]; then
        $TMUX_PLUGIN_MANAGER_PATH/tpm/bin/install_plugins 2>&1 > /dev/null
    fi

    if [[ -d $TMUX_PLUGIN_MANAGER_PATH ]]; then
        cd $TMUX_PLUGIN_MANAGER_PATH
        for plugin in *; do
            cd $plugin
            head=$(git rev-parse HEAD)
            git pull -q --recurse-submodules
            git submodule update --remote -q
            if [[ $(git rev-parse HEAD) != $head ]]; then
                logSub "Updated $plugin"
            fi
            cd ..
        done
    fi
}

# Update asdf plugins
function dotfiles-asdf {
    log "Updating asdf..."

    local asdf_plugins=(${(@f)$(asdf plugin-list)})
    if (( ! $asdf_plugins[(Ie)direnv] )); then
        asdf plugin-add direnv
        logSub "Installed asdf-direnv"
    fi

    makedir $HOME/.asdf/installs/direnv/system/env
    makedir $HOME/.asdf/installs/direnv/system/bin
    link $HOMEBREW_BASE/bin/direnv $HOME/.asdf/installs/direnv/system/bin/direnv

    if (( ! $asdf_plugins[(Ie)nodejs] )); then
        asdf plugin-add nodejs
        bash ~/.asdf/plugins/nodejs/bin/import-release-team-keyring
        logSub "Installed asdf-nodejs"
    fi
    if (( ! $asdf_plugins[(Ie)python] )); then
        asdf plugin-add python
        logSub "Installed asdf-python"
    fi
    # neovim uses node 10 to support at least coc-sh
    if [[ -n $VIM_NODE_VERSION ]] && [[ ! -d $HOME/.asdf/installs/nodejs/$VIM_NODE_VERSION ]]; then
        asdf install nodejs $VIM_NODE_VERSION
        logSub "Installed nodejs $VIM_NODE_VERSION"
    fi

    for dir in $HOME/.asdf/plugins/*; do
        local plugin=${dir:t}
        head=$(git -C $dir rev-parse HEAD)
        git -C $dir pull -q
        if (( $? != 0 )); then
            err "Problem updating $plugin"
        else
            if [[ $(git -C $dir rev-parse HEAD) != $head ]]; then
                logSub "Updated $plugin"
            fi
        fi
    done
}

# Update vim plugins
function dotfiles-vim {
    log "Updating vim plugins..."

    local node_path=$HOMEBREW_BASE/bin
    if [[ -n $VIM_NODE_VERSION ]]; then
        node_path=$HOME/.asdf/installs/nodejs/$VIM_NODE_VERSION/bin
        log "Updating vim's node packages..."
        dotfiles-node $node_path
    fi

    if [[ -a $HOME/.vim/autoload/plug.vim ]]; then
        cd $HOME/.vim/autoload
        existing=$(<plug.vim)
        current=$(curl -s https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim)
        if [[ -z "$current" ]]; then
            err "Problem updating plug.vim"
        elif [[ "$existing" != "$current" ]]; then
            mv plug.vim plug.old
            echo -E "$current" > plug.vim
            logSub "Updated vim-plug"
        fi

        cd $CACHEDIR/vim/plugins
        for plugin in *; do
            cd $plugin
            head=$(git rev-parse HEAD)
            git pull -q --recurse-submodules
            if (( $? != 0 )); then
                err "Problem updating $plugin"
            else
                # If the current head is different than the original head,
                # check for a package.json
                if [[ $(git rev-parse HEAD) != $head ]]; then
                    if [[ -e package.json ]]; then
                        if [[ -e yarn.lock ]]; then
                            $node_path/yarn install --registry=https://registry.npmjs.org --frozen-lockfile --silent
                        else
                            $node_path/npm install --registry=https://registry.npmjs.org --silent
                        fi
                    fi
                    logSub "Updated $plugin"
                fi
            fi
            cd ..
        done
    fi
}

cd $HOME

if [[ -n $1 ]]; then
    if [[ $1 == -h || $1 == --help ]]; then
        echo 'usage: dotfiles [-h | --help | <group>]'
        echo
        echo 'Update local environment. All groups are updated by default.'
        echo
        echo 'Options:'
        echo '    -h, --help     Show this help message'
        echo '    <group>        A group to update'
        echo
        echo 'Available groups:'
        echo '    asdf           Update asdf plugins, install defaults'
        echo '    bat            Rebuild bat theme and filetype cache'
        echo '    brew           Update brew packages, install defaults'
        echo '    home           Update dotfiles and home directories, fixup terminfo database'
        echo '    mas            Update Mac App Store packages'
        echo '    node           Update global node packages, install defaults'
        echo '    python         Update global python packages, install defaults'
        echo '    tmux           Update tpm packages'
        echo '    vim            Update vim plugins'
        echo '    zsh            Update zsh plugins'
    elif [[ $1 == -* ]]; then
        echo "Unknown option $1"
        exit 1
    else
        dotfiles-$1
        log "Done"
    fi
else
    dotfiles-home
    dotfiles-brew

    if (( $+commands[mas] )); then
        dotfiles-mas
    fi

    dotfiles-zsh
    dotfiles-asdf
    dotfiles-python
    dotfiles-node
    dotfiles-tmux
    dotfiles-vim
    dotfiles-bat

    log "Done"
fi
